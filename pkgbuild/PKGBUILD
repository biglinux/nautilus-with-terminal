# Original package from ArchLinux
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

# This version modified to add embedded terminal support
# Maintainer: Bruno Goncalves <biglinux.com.br>

pkgbase=nautilus-with-terminal
pkgname=nautilus-with-terminal
pkgver=48.2
pkgrel=3
conflicts=('nautilus' 'libnautilus-extension')
provides=('nautilus' 'libnautilus-extension' 'libnautilus-extension.so=4-64')
replaces=('nautilus' 'libnautilus-extension')  # CRUCIAL for automatic upgrade
pkgdesc="Default file manager for GNOME with embedded terminal"
url="https://apps.gnome.org/Nautilus/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-autoar
  gnome-desktop-4
  graphene
  gst-plugins-base-libs
  gstreamer
  gtk4
  gvfs
  hicolor-icon-theme
  libadwaita
  libcloudproviders
  libgexiv2
  libportal
  libportal-gtk4
  libx11
  localsearch
  pango
  tinysparql
  vte4
  wayland
  xdg-user-dirs-gtk
)
makedepends=(
  appstream
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
)
checkdepends=(python-gobject)
source=(
  "git+https://gitlab.gnome.org/GNOME/nautilus.git#tag=${pkgver}"
  0001-Disable-tracker-test.patch
  0002-Embedded-terminal.patch
)
b2sums=('SKIP'
        'b70c17cc83308e25fa1d5fcc0c735f82ba641fa6f78a064c5b01d65fa5b45505d23247528e2d97254a6983e939e3e9e1b1a737f3aab2546e5ecc825f954d9dcc'
        '46e3db153a926a07ed702ad6769abfef20032b98fc9f5c2b5c4d039f4bfefc423a51b25e6ed13a2005d8b8066d7e922ae59e27347a2f39c08f3a1ce3884fa596')

prepare() {
  cd nautilus

  # Tracker test is broken in our build containers
  git apply -3 ../0001-Disable-tracker-test.patch

  # Add embedded terminal, opened using the F4 key
  git apply --3way --union ../0002-Embedded-terminal.patch
}

build() {
  local meson_options=(
    -D docs=true
    -D packagekit=false
  )

  arch-meson nautilus build "${meson_options[@]}"
  meson compile -C build
}

package() {
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"
  
  # Check if all critical libraries are present
  local required_files=(
    "usr/lib/libnautilus-extension.so.4"
    "usr/lib/girepository-1.0/Nautilus-4.0.typelib"
    "usr/include/nautilus/libnautilus-extension"
  )
  
  for file in "${required_files[@]}"; do
    if [[ ! -e "$pkgdir/$file" ]]; then
      error "Critical file not found: $file"
      error "This may break dependent packages like seahorse-nautilus"
      return 1
    fi
  done
  
  # Debug log during build
  msg2 "Installed libnautilus-extension files:"
  find "$pkgdir" -name "*nautilus-extension*" -type f | head -10
}

# vim:set sw=2 sts=-1 et:

