# Original package from ArchLinux
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

# This version modified to add embedded terminal support
# Maintainer: Bruno Goncalves <biglinux.com.br>

pkgbase=nautilus-with-terminal
pkgname=nautilus-with-terminal
pkgver=49.0
pkgrel=1
conflicts=('nautilus' 'libnautilus-extension')
provides=('nautilus' 'libnautilus-extension' 'libnautilus-extension.so=4-64')
replaces=('nautilus' 'libnautilus-extension')
pkgdesc="Default file manager for GNOME with embedded terminal"
url="https://apps.gnome.org/Nautilus/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-autoar
  gnome-desktop-4
  graphene
  gst-plugins-base-libs
  gstreamer
  gtk4
  gvfs
  hicolor-icon-theme
  libadwaita
  libcloudproviders
  libgexiv2
  libportal
  libportal-gtk4
  libx11
  localsearch
  pango
  tinysparql
  vte4
  wayland
  xdg-user-dirs-gtk
)
makedepends=(
  appstream
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
)
checkdepends=(python-gobject)
source=(
  "git+https://gitlab.gnome.org/GNOME/nautilus.git#tag=${pkgver}"
  0002-bigbruno-all-modifications.patch
)
b2sums=(
        'SKIP'
        'a67154974aa978f03283968443802a0edcb53129293858fa00da707d438c8f401735573b2e369ba711cfbb69cbfd6464ad1b6d8af3d06963a965eabf83347dc9'
)

prepare() {
  cd nautilus

  # Apply ALL bigbruno's modifications (terminal + other enhancements)
  git apply --3way ../0002-bigbruno-all-modifications.patch
}

build() {
  local meson_options=(
    -D docs=true
    -D packagekit=false
  )

  arch-meson nautilus build "${meson_options[@]}"
  meson compile -C build
}

package() {
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"
  
  # Check if all critical libraries are present
  local required_files=(
    "usr/lib/libnautilus-extension.so.4"
    "usr/lib/girepository-1.0/Nautilus-4.0.typelib"
    "usr/include/nautilus/libnautilus-extension"
  )
  
  for file in "${required_files[@]}"; do
    if [[ ! -e "$pkgdir/$file" ]]; then
      error "Critical file not found: $file"
      error "This may break dependent packages like seahorse-nautilus"
      return 1
    fi
  done
}

# vim:set sw=2 sts=-1 et:
